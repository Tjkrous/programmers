//connect USB 0
usb 0

//set SWD
si 1

//set device
device NRF51822_XXAA

//reset
r

//save the MAC from UICR + 0x80
savebin mac_addr.bin 0x10001080 8

//set device
device NRF51822_XXAA

//reset
r

//erase all flash
w4 4001e504 2
w4 4001e50c 1
sleep 200

//commit erase with reset
r

// enable the flash for writing
w4 4001e504 1

//write bootloader and softdevice
loadbin binaries\s110_8.0.0.bin 0x0
loadbin binaries\rigdfu2_nrf51_s110_rel_3_2_1.bin 0x3a800
loadbin binaries\bmdware_rel_nrf51_s110_eval_3_1_1.bin 0x18000
loadbin binaries\settings_s110_8.0.0_bmdware_eval_3_1_1.bin 0x3f800
loadbin binaries\rigdfu2_uicr.bin 0x10001014
loadbin mac_addr.bin 0x10001080

sleep 200

//commit writes with reset
r

//verify
verifybin binaries\s110_8.0.0.bin 0x0
verifybin binaries\rigdfu2_nrf51_s110_rel_3_2_1.bin 0x3a800
verifybin binaries\bmdware_rel_nrf51_s110_eval_3_1_1.bin 0x18000
verifybin binaries\settings_s110_8.0.0_bmdware_eval_3_1_1.bin 0x3f800
verifybin binaries\rigdfu2_uicr.bin 0x10001014
verifybin mac_addr.bin 0x10001080

// enable flash writes then enable readback protection
w4 4001e504 1
loadbin binaries\protect.bin 0x10001004
sleep 200

//reset and go
r
g
sleep 200
exit
